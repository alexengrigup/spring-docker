plugins {
    id 'org.springframework.boot' version '3.1.2' apply false
    id 'io.spring.dependency-management' version '1.1.2' apply false
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'dev.alexengrig.sample'
    version = '0-SNAPSHOT'
    sourceCompatibility = '17'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2022.0.4'
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'io.micrometer:micrometer-tracing-bridge-otel'
        runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    test {
        useJUnitPlatform()
    }

    springBoot {
        buildInfo()
    }

    bootBuildImage {
        imageName = "docker.io/alexengrig/${project.name}:${project.version}"
        environment = [
                'BP_OPENTELEMETRY_ENABLED': 'true',
        ]
        buildpacks = [
                'gcr.io/paketo-buildpacks/java',
                'gcr.io/paketo-buildpacks/opentelemetry',
        ]
        verboseLogging = true
    }

}
